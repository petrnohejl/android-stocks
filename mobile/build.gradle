// Main build script for Stocks
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK
//        gradlew test                    Run local unit test
//        gradlew connectedAndroidTest    Run instrumented unit test


final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 0 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits


apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.fernandocejas.frodo"


dependencies
{
	compile "com.android.support:support-compat:25.2.0"
	compile "com.android.support:support-core-utils:25.2.0"
	compile "com.android.support:support-core-ui:25.2.0"
	compile "com.android.support:support-fragment:25.2.0"
	compile "com.android.support:appcompat-v7:25.2.0"
	compile "com.android.support:recyclerview-v7:25.2.0"
	compile "com.android.support:support-annotations:25.2.0"
	compile "com.android.support:design:25.2.0"
	compile "com.android.support.constraint:constraint-layout:1.0.0"
	compile "com.github.bumptech.glide:glide:3.7.0"
	compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
	compile "com.google.code.gson:gson:2.8.0"
	compile "com.squareup.okhttp3:okhttp:3.6.0"
	compile "com.squareup.okhttp3:logging-interceptor:3.6.0"
	compile "com.squareup.retrofit2:retrofit:2.2.0"
	compile "com.squareup.retrofit2:adapter-rxjava2:2.2.0"
	compile "com.squareup.retrofit2:converter-gson:2.2.0"
	compile "eu.inloop:androidviewmodel:1.3.1"
	compile "io.reactivex.rxjava2:rxandroid:2.0.1"
	compile "io.reactivex.rxjava2:rxjava:2.0.6"
	compile "org.alfonz:alfonz-adapter:0.3.0"
	compile "org.alfonz:alfonz-graphics:0.3.0"
	compile "org.alfonz:alfonz-mvvm:0.3.0"
	compile "org.alfonz:alfonz-rest:0.3.0"
	compile "org.alfonz:alfonz-rx:0.3.0"
	compile "org.alfonz:alfonz-utility:0.3.0"
	compile "org.alfonz:alfonz-view:0.3.0"
	compile fileTree(dir: "libs", include: ["*.jar"])
	debugCompile "com.squareup.leakcanary:leakcanary-android:1.5"
	releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"
	testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-core:2.2.27"
}


android
{
	compileSdkVersion 25
	buildToolsVersion "25.0.2"

	defaultConfig
	{
		applicationId "com.example.stocks"
		minSdkVersion 16
		targetSdkVersion 25
		versionCode VERSION_MAJOR * 10000000 + VERSION_MINOR * 100000 + VERSION_PATCH * 1000 + VERSION_BUILD
		versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
	}

	signingConfigs
	{
		release
		{
			// passwords and alias are obtained via askForPasswords task
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes
	{
		debug
		{
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

			versionNameSuffix "-debug"
		}

		release
		{
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions
	{
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	dataBinding
	{
		enabled = true
	}

	applicationVariants.all
	{
		variant ->
			if(variant.buildType.name.equals("release"))
			{
				renameArtifact(variant)
			}
	}
}


def renameArtifact(variant)
{
	variant.outputs.each
	{
		output ->
			def date = new Date().format("yyyyMMdd")
			def fileName = "stocks-${project.name}-${variant.versionName}-${variant.versionCode}-${date}-${variant.name}.apk"
			output.outputFile = new File((String) output.outputFile.parent, fileName)
	}
}


task askForPasswords <<
{
	def storePass
	def keyAlias
	def keyPass

	def keystorePropertiesFile = new File((String) project.property("keystore.properties"))

	if(project.hasProperty("keystore.properties") && keystorePropertiesFile.exists())
	{
		println "Loading keystore passwords from property file..."
		Properties properties = new Properties()
		properties.load(new FileInputStream(keystorePropertiesFile))
		storePass = properties["keystore.store.password"]
		keyAlias = properties["keystore.key.alias"]
		keyPass = properties["keystore.key.password"]
	}
	else
	{
		println "Getting keystore passwords from user input..."
		storePass = new String(System.console().readPassword("\nStore password: "))
		keyAlias = new String(System.console().readLine("Key alias: "))
		keyPass = new String(System.console().readPassword("Key password: "))
	}

	android.signingConfigs.release.storePassword = storePass
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPass
}


tasks.whenTaskAdded
{
	theTask ->
		// the name is "packageRelease" with no flavor
		// in case of flavors, the names could vary, e.g. "packageDemoRelease", ...
		if(theTask.name.startsWith("package") && theTask.name.endsWith("Release"))
		{
			theTask.dependsOn "askForPasswords"
		}
}
